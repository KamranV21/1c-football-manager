#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ОбновитьФутболистов() Экспорт
	
	НавыкиПоПозициям = ИгровыеМеханики.КлючевыеНавыкиПозиций();
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Футболисты.Ссылка КАК Футболист,
	                      |	Футболисты.Потенциал КАК Потенциал,
	                      |	Футболисты.Рейтинг КАК Рейтинг,
	                      |	Футболисты.Позиция КАК Позиция
	                      |ИЗ
	                      |	Справочник.Футболисты КАК Футболисты");
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ФутболистОбъект = Выборка.Футболист.ПолучитьОбъект(); 
		
		ФутболистОбъект.Возраст = ФутболистОбъект.Возраст + 1;
		
		ВозрастЗрелостиФутболиста = 24;
		ПерспективныеГоды = ВозрастЗрелостиФутболиста - ФутболистОбъект.Возраст; 
		
		Если ПерспективныеГоды > 0 Тогда

			ПотенциалРостаЗаСезон = (ФутболистОбъект.Потенциал - ФутболистОбъект.Рейтинг) / ПерспективныеГоды;
			
			Если ПотенциалРостаЗаСезон > 0 Тогда		
				
				Навыки = НавыкиПоПозициям[Выборка.Позиция];
				
				Для Каждого КлючИЗначение Из Навыки Цикл 			
					КоэффициентДеления = ?(КлючИЗначение.Значение < 10, 2, 1);		
					ФутболистОбъект[КлючИЗначение.Ключ] = ФутболистОбъект[КлючИЗначение.Ключ] + ИгровыеМеханики.СлучайноеЧисло(1, ПотенциалРостаЗаСезон / КоэффициентДеления);			
				КонецЦикла;   
				
			КонецЕсли;
			
		КонецЕсли; 
		
		Если ФутболистОбъект.Возраст >= 30 Тогда
			
			Навыки = НавыкиПоПозициям[Выборка.Позиция];
			
			Для Каждого КлючИЗначение Из Навыки Цикл 			
				ПроцентПаденияНавыка = ?(КлючИЗначение.Значение < 10, ИгровыеМеханики.СлучайноеЧисло(0, 2), ИгровыеМеханики.СлучайноеЧисло(0, 3));		
				ФутболистОбъект[КлючИЗначение.Ключ] = ФутболистОбъект[КлючИЗначение.Ключ] - ФутболистОбъект[КлючИЗначение.Ключ] / 100 * ПроцентПаденияНавыка;			
			КонецЦикла; 
			
		КонецЕсли;
		
		ФутболистОбъект.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗавершитьКарьеруФутболистовИСоздатьРегенов() Экспорт
	
	ВсеИменаФутболистов = ВсеИменаФутболистов();
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СправочникФутбольныеКлубы.Ссылка КАК ФутбольныйКлуб,
	                      |	ОКР(СУММА(тСтартовыйСостав.Футболист.Рейтинг) / 11, 0) КАК Рейтинг
	                      |ПОМЕСТИТЬ ВтРейтинги
	                      |ИЗ
	                      |	Справочник.ФутбольныеКлубы КАК СправочникФутбольныеКлубы
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФутбольныеКлубы.СтартовыйСостав КАК тСтартовыйСостав
	                      |		ПО (тСтартовыйСостав.Ссылка = СправочникФутбольныеКлубы.Ссылка)
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	СправочникФутбольныеКлубы.Ссылка
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	Футболисты.Ссылка КАК Футболист,
	                      |	ВтРейтинги.Рейтинг КАК Рейтинг
	                      |ИЗ
	                      |	Справочник.Футболисты КАК Футболисты
	                      |		ЛЕВОЕ СОЕДИНЕНИЕ ВтРейтинги КАК ВтРейтинги
	                      |		ПО Футболисты.ФутбольныйКлуб = ВтРейтинги.ФутбольныйКлуб
	                      |ГДЕ
	                      |	Футболисты.Возраст >= 34
	                      |	И НЕ Футболисты.ЗавершилКарьеру");
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ЗавершилКарьеру = ИгровыеМеханики.СлучайноеЧисло(0, 1) = 1;
		Если ЗавершилКарьеру Тогда 
			
			ФутболистОбъект = Выборка.Футболист.ПолучитьОбъект();
			ФутболистОбъект.ЗавершилКарьеру = Истина;   
			ФутбольныйКлуб = ФутболистОбъект.ФутбольныйКлуб;
			ФутболистОбъект.ФутбольныйКлуб = Справочники.ФутбольныеКлубы.ПустаяСсылка();
			ФутболистОбъект.Записать();
			
			Имя = СтрРазделить(ВсеИменаФутболистов[ИгровыеМеханики.СлучайноеЧисло(0, ВсеИменаФутболистов.Количество() - 1)], " ")[0];
			Фамилия = СтрРазделить(ВсеИменаФутболистов[ИгровыеМеханики.СлучайноеЧисло(0, ВсеИменаФутболистов.Количество() - 1)], " ")[1];
			
			ИмяРегена = Имя + " " + Фамилия;
			
			Реген = СоздатьРегена(ИмяРегена, ФутбольныйКлуб, ФутболистОбъект.Позиция, Выборка.Рейтинг);
			
			ФутбольныйКлубОбъект = ФутбольныйКлуб.ПолучитьОбъект();
			Для Каждого Строка Из ФутбольныйКлубОбъект.СтартовыйСостав Цикл
				Если Строка.Футболист = Выборка.Футболист Тогда
					Строка.Футболист = Реген;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла; 

КонецПроцедуры   

#КонецОбласти	

#Область СлужебныеПроцедурыИФункции

Функция СоздатьРегена(Наименование, ФутбольныйКлуб, Позиция, СреднийУровеньКлуба)

	МаксимальноеЗначениеНавыка = СреднийУровеньКлуба - 5;
	МинимальноеЗначениеНавыка = СреднийУровеньКлуба - 15;
	
	ФутболистОбъект = Справочники.Футболисты.СоздатьЭлемент(); 
	ФутболистОбъект.Гражданство = Справочники.СтраныМира.Россия;
	ФутболистОбъект.Возраст = ИгровыеМеханики.СлучайноеЧисло(17, 19); 
	ФутболистОбъект.Позиция = Позиция;
	ФутболистОбъект.Наименование = Наименование;
	ФутболистОбъект.ФутбольныйКлуб = ФутбольныйКлуб;
	ФутболистОбъект.Фотография = Новый ХранилищеЗначения(БиблиотекаКартинок["ФотоФутболиста" + ИгровыеМеханики.СлучайноеЧисло(1, 30)]);
	
	НавыкиПоПозициям = ИгровыеМеханики.КлючевыеНавыкиПозиций();
	Навыки = НавыкиПоПозициям[Позиция];
	
	Для Каждого КлючИЗначение Из Навыки Цикл
		
		ЛотереяНавыков = ИгровыеМеханики.СлучайноеЧисло(1, 21); 
		Если КлючИЗначение.Значение > 10 И ЛотереяНавыков = 21 Тогда
			ЗначениеНавыка = ИгровыеМеханики.СлучайноеЧисло(МинимальноеЗначениеНавыка, 99);
		Иначе
			ЗначениеНавыка = ИгровыеМеханики.СлучайноеЧисло(МинимальноеЗначениеНавыка, МаксимальноеЗначениеНавыка) * ?(КлючИЗначение.Значение < 0.5, КлючИЗначение.Значение, 1);
		КонецЕсли;
		
		ФутболистОбъект[КлючИЗначение.Ключ] = ЗначениеНавыка;   
		
	КонецЦикла;
	
	ФутболистОбъект.Записать(); 
	
	ВероятностьПрогресса = ИгровыеМеханики.СлучайноеЧисло(1, 99); 
	МаксимальныйПрогресс = Макс(ВероятностьПрогресса, ФутболистОбъект.Рейтинг + ИгровыеМеханики.СлучайноеЧисло(1, 10));
	
	ФутболистОбъект.Потенциал = ИгровыеМеханики.СлучайноеЧисло(ФутболистОбъект.Рейтинг, МаксимальныйПрогресс);	
	ФутболистОбъект.Записать();
	
	Возврат ФутболистОбъект.Ссылка;
				
КонецФункции

Функция ВсеИменаФутболистов()

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Футболисты.Наименование КАК Наименование
	                      |ИЗ
	                      |	Справочник.Футболисты КАК Футболисты");
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Наименование");
	
КонецФункции 

#КонецОбласти	

#КонецЕсли

