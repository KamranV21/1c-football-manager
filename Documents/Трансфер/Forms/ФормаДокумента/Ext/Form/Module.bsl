
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьТрансферныйБюджет();
	
	ЗаполнитьТекущуюТрансфернуюСтоимость();	
	
	ВывестиФотографию();
	
	УстановитьВидимостьЭлементовФормы();
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатусПереговоровПродажаПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементовФормы();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтправитьПредложение(Команда)

	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Объект.ОжидаетсяДействиеИгры = Истина;
	СтрокаИстории = Объект.ИсторияПереговоров.Добавить();
	СтрокаИстории.ТекстПредложения = Объект.Предложение; 
	
	ОтправитьПредложениеНаСервере();
	УстановитьВидимостьЭлементовФормы(); 
	ОбновитьТрансферныйБюджет();
	
	Модифицированность = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПрекратитьПереговоры(Команда)   
	
	ПрекратитьПереговорыНаСервере();  

	УстановитьВидимостьЭлементовФормы(); 
	ОбновитьТрансферныйБюджет();
	
	Модифицированность = Ложь;   
	
	Оповестить("ОбновитьПереговоры");

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтвет(Команда)  
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ОтправитьОтветНаСервере();  
	
	УстановитьВидимостьЭлементовФормы(); 
	ОбновитьТрансферныйБюджет();
	
	Модифицированность = Ложь;   
	
	Оповестить("ОбновитьПереговоры");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОтправитьПредложениеНаСервере() 
	
	Объект.Дата = Константы.ИгроваяДата.Получить();
	
	Значение = РеквизитФормыВЗначение("Объект");
	Значение.Записать(РежимЗаписиДокумента.Проведение); 
	ЗначениеВРеквизитФормы(Значение, "Объект");
			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТекущуюТрансфернуюСтоимость()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТрансфернаяСтоимостьСрезПоследних.ТрансфернаяСтоимость КАК ТрансфернаяСтоимость
	                      |ИЗ
	                      |	РегистрСведений.ТрансфернаяСтоимость.СрезПоследних(, Футболист = &Футболист) КАК ТрансфернаяСтоимостьСрезПоследних");
	Запрос.УстановитьПараметр("Футболист", Объект.Футболист);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ТрансфернаяСтоимость = Выборка.ТрансфернаяСтоимость;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	ФутбольныйКлубМенеджера = Константы.ФутбольныйКлубМенеджера.Получить();
	
	Если Объект.КлубПокупатель = ФутбольныйКлубМенеджера Тогда
		
		Элементы.ГруппаПродажа.Видимость = Ложь; 
		
		Отклонено = Объект.СтатусПереговоров = Перечисления.СтатусПереговоров.Отклонено;
		ОжидаетсяДействиеИгрока = Не Объект.ОжидаетсяДействиеИгры И Не Отклонено;
		
		Элементы.Предложение.ТолькоПросмотр = Не ОжидаетсяДействиеИгрока;
		Элементы.ОтправитьПредложение.Видимость = ОжидаетсяДействиеИгрока; 
		
		Элементы.ДекорацияПодсказка.Видимость = Объект.ОжидаетсяДействиеИгры; 
		
		Элементы.ОтветноеПредложение.Видимость = Объект.СтатусПереговоров = Перечисления.СтатусПереговоров.ОтветноеПредложение;	
		Элементы.ПрекратитьПереговоры.Видимость = Объект.СтатусПереговоров = Перечисления.СтатусПереговоров.ОтветноеПредложение И Не Объект.ОжидаетсяДействиеИгры;	
		
	ИначеЕсли Объект.КлубПродавец = ФутбольныйКлубМенеджера Тогда	
		
		Элементы.ГруппаПокупка.Видимость = Ложь; 
		
		Если Не Объект.ОжидаетсяДействиеИгры Тогда
			ТолькоПросмотр = Истина; 
			Элементы.ОтправитьОтвет.Видимость = Ложь;
		КонецЕсли;  
		
		Элементы.ОтветноеПредложениеПродажа.Видимость = Объект.СтатусПереговоров = Перечисления.СтатусПереговоров.ОтветноеПредложение; 
		
	Иначе    
		
		Элементы.ГруппаПродажа.Видимость = Ложь;
		
		ТолькоПросмотр = Истина; 
		Элементы.ТрансферныйБюджет.Видимость = Ложь;	
		Элементы.ОтправитьПредложение.Видимость = Ложь;	
		Элементы.ПрекратитьПереговоры.Видимость = Ложь;	
		Элементы.ДекорацияПодсказка.Видимость = Ложь; 

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьТрансферныйБюджет()

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	БюджетыФутбольныхКлубовОстатки.ТрансферныйБюджетОстаток КАК ТрансферныйБюджетОстаток
	                      |ИЗ
	                      |	РегистрНакопления.БюджетыФутбольныхКлубов.Остатки(, ФутбольныйКлуб = &ФутбольныйКлуб) КАК БюджетыФутбольныхКлубовОстатки");
	Запрос.УстановитьПараметр("ФутбольныйКлуб", Константы.ФутбольныйКлубМенеджера.Получить());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		ТрансферныйБюджет = Выборка.ТрансферныйБюджетОстаток;
	Иначе
		ТрансферныйБюджет = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрекратитьПереговорыНаСервере(ТекстОтвета = "")
	
	Объект.Дата = Константы.ИгроваяДата.Получить();
	Объект.СтатусПереговоров = Перечисления.СтатусПереговоров.Отклонено;    
	Объект.ИсторияПереговоров[Объект.ИсторияПереговоров.Количество() - 1].ТекстОтвета = ТекстОтвета;

	Значение = РеквизитФормыВЗначение("Объект");
	Значение.Записать(РежимЗаписиДокумента.Проведение); 
	ЗначениеВРеквизитФормы(Значение, "Объект");
			
КонецПроцедуры

&НаСервере
Процедура ОтправитьОтветНаСервере()
	
	Если Не Трансферы.ТрансферТехническиВозможен(Объект.Футболист) Тогда
		ТекстОтвета = НСтр("ru = 'Мы не можем остаться без футболистов. Трансфер невозможен.'");
		ПрекратитьПереговорыНаСервере(ТекстОтвета);
	КонецЕсли;
	
	Объект.Дата = Константы.ИгроваяДата.Получить();
	Объект.ОжидаетсяДействиеИгры = Ложь;
	Объект.ИсторияПереговоров[Объект.ИсторияПереговоров.Количество() - 1].ТекстОтвета = 
		Трансферы.ТекстОтветаПереговоров(Объект.СтатусПереговоров, Объект.ОтветноеПредложение);

	Значение = РеквизитФормыВЗначение("Объект");
	Значение.Записать(РежимЗаписиДокумента.Проведение); 
	ЗначениеВРеквизитФормы(Значение, "Объект");
	
КонецПроцедуры

&НаСервере
Процедура ВывестиФотографию()

	АдресФотографии = ПолучитьНавигационнуюСсылку(Объект.Футболист, "Фотография"); 

КонецПроцедуры

#КонецОбласти
