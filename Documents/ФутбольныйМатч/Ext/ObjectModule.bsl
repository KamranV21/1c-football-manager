
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда		

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Запрос = Новый Запрос;
	ИнициализацияПараметровЗапроса(Запрос);	  
	
	ТекстыЗапросов = Новый Структура;
	ТекстЗапросаТурнирнаяТаблица(ТекстыЗапросов);
	ТекстЗапросаСтатистикаФутболистов(ТекстыЗапросов);
	
	РезультатыЗапроса = РезультатыЗапроса(Запрос, ТекстыЗапросов);
	
	СформироватьДвиженияПоРегиструТурнирнаяТаблица(РезультатыЗапроса, Движения);
	СформироватьДвиженияПоРегиструСтатистикаФутболистов(РезультатыЗапроса, Движения);
	
КонецПроцедуры  

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти	

#Область СлужебныеПроцедурыИФункции

Процедура ИнициализацияПараметровЗапроса(Запрос)

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

КонецПроцедуры

Процедура ТекстЗапросаТурнирнаяТаблица(ТекстыЗапросов)

	ТекстЗапроса = "ВЫБРАТЬ
	               |	ФутбольныйМатч.Дата КАК Период,
	               |	ФутбольныйМатч.Ссылка КАК Регистратор,
	               |	ФутбольныйМатч.Лига КАК Лига,
	               |	ФутбольныйМатч.Сезон КАК Сезон,
	               |	ФутбольныйМатч.Хозяева КАК ФутбольныйКлуб,
	               |	ФутбольныйМатч.ОчкиХозяев КАК Очки,
	               |	ВЫБОР
	               |		КОГДА ФутбольныйМатч.ОчкиХозяев = 3
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Выигрыши,
	               |	ВЫБОР
	               |		КОГДА ФутбольныйМатч.ОчкиХозяев = 1
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Ничьи,
	               |	ВЫБОР
	               |		КОГДА ФутбольныйМатч.ОчкиХозяев = 0
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ КАК Порожения,
	               |	ФутбольныйМатч.ГолыХозяев КАК Забито,
	               |	ФутбольныйМатч.ГолыГостей КАК Пропущено,
	               |	1 КАК СыграноМатчей
	               |ИЗ
	               |	Документ.ФутбольныйМатч КАК ФутбольныйМатч
	               |ГДЕ
	               |	ФутбольныйМатч.Ссылка = &Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ФутбольныйМатч.Дата,
	               |	ФутбольныйМатч.Ссылка,
	               |	ФутбольныйМатч.Лига,
	               |	ФутбольныйМатч.Сезон,
	               |	ФутбольныйМатч.Гости,
	               |	ФутбольныйМатч.ОчкиГостей,
	               |	ВЫБОР
	               |		КОГДА ФутбольныйМатч.ОчкиГостей = 3
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА ФутбольныйМатч.ОчкиГостей = 1
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА ФутбольныйМатч.ОчкиГостей = 0
	               |			ТОГДА 1
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ФутбольныйМатч.ГолыГостей,
	               |	ФутбольныйМатч.ГолыХозяев,
	               |	1
	               |ИЗ
	               |	Документ.ФутбольныйМатч КАК ФутбольныйМатч
	               |ГДЕ
	               |	ФутбольныйМатч.Ссылка = &Ссылка";
	ТекстыЗапросов.Вставить("ТурнирнаяТаблица", ТекстЗапроса);

КонецПроцедуры 

Процедура ТекстЗапросаСтатистикаФутболистов(ТекстыЗапросов)

	ТекстЗапроса = "ВЫБРАТЬ
	               |	ФутбольныйМатч.Ссылка.Дата КАК Период,
	               |	ФутбольныйМатч.Ссылка КАК Регистратор,
	               |	ФутбольныйМатч.Ссылка.Лига КАК Лига,
	               |	ФутбольныйМатч.Ссылка.Сезон КАК Сезон,
	               |	ФутбольныйМатч.ФутбольныйКлуб КАК ФутбольныйКлуб,
	               |	ФутбольныйМатч.АвторГола КАК Футболист,
	               |	1 КАК Голы,
	               |	0 КАК ГолевыеПередачи,
	               |	1 КАК СыграноМатчей
	               |ИЗ
	               |	Документ.ФутбольныйМатч.Голы КАК ФутбольныйМатч
	               |ГДЕ
	               |	ФутбольныйМатч.Ссылка = &Ссылка
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ФутбольныйМатч.Ссылка.Дата,
	               |	ФутбольныйМатч.Ссылка,
	               |	ФутбольныйМатч.Ссылка.Лига,
	               |	ФутбольныйМатч.Ссылка.Сезон,
	               |	ФутбольныйМатч.ФутбольныйКлуб,
	               |	ФутбольныйМатч.ГолеваяПередача,
	               |	0,
	               |	1,
	               |	1
	               |ИЗ
	               |	Документ.ФутбольныйМатч.Голы КАК ФутбольныйМатч
	               |ГДЕ
	               |	ФутбольныйМатч.Ссылка = &Ссылка";
	ТекстыЗапросов.Вставить("СтатистикаФутболистов", ТекстЗапроса);

КонецПроцедуры

Функция	РезультатыЗапроса(Запрос, ТекстыЗапросов)

	ТекстИтоговогоЗапроса = "";
	Для Каждого КлючИЗначение Из ТекстыЗапросов Цикл
		ТекстИтоговогоЗапроса = ТекстИтоговогоЗапроса + 
				?(ЗначениеЗаполнено(ТекстИтоговогоЗапроса), "; ", "") + 
				КлючИЗначение.Значение;	
	КонецЦикла; 
	
	Запрос.Текст = ТекстИтоговогоЗапроса;
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	РезультатыЗапроса = Новый Структура;
	
	Индекс = 0;
	Для Каждого КлючИЗначение Из ТекстыЗапросов Цикл
		РезультатыЗапроса.Вставить(КлючИЗначение.Ключ, МассивРезультатов[Индекс]);		
		Индекс = Индекс + 1;		
	КонецЦикла;
	
	Возврат РезультатыЗапроса;
	
КонецФункции

Процедура СформироватьДвиженияПоРегиструТурнирнаяТаблица(РезультатыЗапроса, Движения)

	Выборка = РезультатыЗапроса.ТурнирнаяТаблица.Выбрать();           
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.ТурнирнаяТаблица.Добавить(), Выборка);
	КонецЦикла;                                                       
	
	Движения.ТурнирнаяТаблица.Записать();

КонецПроцедуры 

Процедура СформироватьДвиженияПоРегиструСтатистикаФутболистов(РезультатыЗапроса, Движения)

	Выборка = РезультатыЗапроса.СтатистикаФутболистов.Выбрать();           
	Пока Выборка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Движения.СтатистикаФутболистов.Добавить(), Выборка);
	КонецЦикла;                                                       
	
	Движения.СтатистикаФутболистов.Записать();

КонецПроцедуры

#КонецОбласти	

#КонецЕсли
