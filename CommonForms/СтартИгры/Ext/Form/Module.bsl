   
#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Константы.ИгроваяДата.Получить()) Тогда
		Константы.ИгроваяДата.Установить('20240701');	                 
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Константы.ТекущийСезон.Получить()) Тогда
		Константы.ТекущийСезон.Установить(Справочники.Сезоны.Сезон2425);	                 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ПринудительноеЗакрытие Тогда
		Возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти   
   
#Область ОбработчикиСобытийРеквизитовШапки

&НаКлиенте
Процедура ЛигаПриИзменении(Элемент)
	
	ЛигаПриИзмененииНаСервере();	
	
КонецПроцедуры 

&НаКлиенте
Процедура ФутбольныйКлубПриИзменении(Элемент)

	ФутбольныйКлубПриИзмененииНаСервере();

КонецПроцедуры 

#КонецОбласти   

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Начать(Команда)
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	НачатьНаСервере(); 
	
	ПринудительноеЗакрытие = Истина;
	
	Оповестить("СтартИгры");
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти   

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЛигаПриИзмененииНаСервере()

	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СоставЛиги.ФутбольныйКлуб КАК ФутбольныйКлуб
	                      |ИЗ
	                      |	РегистрСведений.СоставЛиги КАК СоставЛиги
	                      |ГДЕ
	                      |	СоставЛиги.Лига = &Лига
	                      |	И СоставЛиги.Сезон = &Сезон");
	Запрос.УстановитьПараметр("Лига", Лига);
	Запрос.УстановитьПараметр("Сезон", Константы.ТекущийСезон.Получить());
	
	ФутбольныеКлубы = Новый Массив;
	
	Выборка = Запрос.Выполнить().Выбрать(); 
	Пока Выборка.Следующий() Цикл		
		ФутбольныеКлубы.Добавить(Выборка.ФутбольныйКлуб);				
	КонецЦикла;
	
	ДоступныеЗначения = Новый ФиксированныйМассив(ФутбольныеКлубы);

	ПараметрВыбора = Новый ПараметрВыбора("Отбор.Ссылка", ДоступныеЗначения);
	
	ПараметрыВыбораКлуба = Новый Массив;
	ПараметрыВыбораКлуба.Добавить(ПараметрВыбора);
	
	Элементы.ФутбольныйКлуб.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораКлуба);
	
КонецПроцедуры

&НаСервере
Процедура ФутбольныйКлубПриИзмененииНаСервере()
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СоставЛиги.Лига КАК Лига
	                      |ИЗ
	                      |	РегистрСведений.СоставЛиги КАК СоставЛиги
	                      |ГДЕ
	                      |	СоставЛиги.ФутбольныйКлуб = &ФутбольныйКлуб
	                      |	И СоставЛиги.Сезон = &Сезон");
	Запрос.УстановитьПараметр("ФутбольныйКлуб", ФутбольныйКлуб);
	Запрос.УстановитьПараметр("Сезон", Константы.ТекущийСезон.Получить());

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Лига = Выборка.Лига;
		ЛигаПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НачатьНаСервере()
	
	Константы.ЛигаМенеджера.Установить(Лига);
	Константы.ФутбольныйКлубМенеджера.Установить(ФутбольныйКлуб);
		
КонецПроцедуры

#КонецОбласти   
